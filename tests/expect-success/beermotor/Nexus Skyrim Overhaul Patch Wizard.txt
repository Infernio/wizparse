;== Nexus Skyrim Overhaul Patches BAIN wizard by Mertz ===
; Special thanks to Alt3rn1ty

RequireVersions "", "", "", "307"
DeSelectAll
;== Welcome screen ==

SelectOne "Nexus Skyrim Overhaul Patches - BAIN Install Wizard",\
    "|Welcome","Nexus Skyrim Overhaul Patches\n\nCompatibility Patches Provided:\n\nMain Nexus Skyrim Overhaul Patches:\n- Mods that edit \"No Stone Unturned\" \(This simply removes the Barenziah Quest Markers from the original.\)\n- For those who prefer Yorimi's take on the Companions requirements over Taj\'s. By SkyLover264.\n\nFor consistency and compatibility with other mods:\n - The Magic of Food and Drink\n - Advanced Adversary Encounters - Ultimate Edition\n - Elemental Destruction Magic\n - Imperious - Races of Skyrim\n - Vampire Lord and Werewolf Hybrid","fomod\\images\\ELD.jpg",\
    "Credits","Nexus Skyrim Overhaul Patches by SkyLover264 and Kelsenellenelvian\nBAIN Wizard by Mertz\n\nClick next to continue the installation","fomod\\images\\ELD.jpg"
EndSelect

; Start testing for the presence of ESP/ESM files in the data directory to install mods.

; Note: this first mod checks for "BarenziahQuestMarkers.esp" because that's at least one mod that 
; modifies the No Stone Unturne quest. Otherwise the user can manually select it.
sModName101 = "Replacement esp without Stones of Barenziah Markers"
sModDescription101 = "For compatibility with other mods that edit No Stone Unturned. By SkyLover264."
sModImage101 = "Fomod\\Images\\NSB.jpg"
If GetEspmStatus("BarenziahQuestMarkers.esp") > 1
	sModName101 = "|" + sModName101
EndIf

sModName102 = "Slower Companions Advancement - Original Requirements"
sModDescription102 = "For those who prefer Yorimi's take on the Companions requirements over Taj's. By SkyLover264."
sModImage102 = "Fomod\\Images\\SCA.jpg"

sModName103 = "Advanced Adversary Encounters"
sModDescription103 = "Patch for Advanced Adversary Encounters. Patch by SirJesto."
sModImage103 = "fomod\\images\\AAE.jpg"
If GetEspmStatus("AAE Ultimate Edition.esp") > 1
	sModName103 = "|" + sModName102
EndIf

sModName104 = "Elemental Destruction Magic"
sModDescription104 = "Patch for Elemental Destruction Magic. Patch by SkyLover264."
sModImage104 = "fomod\\images\\ELD.jpg"
If GetEspmStatus("Elemental Destruction.esp") > 1
	sModName104 = "|" + sModName104
EndIf

sModName105= "Imperious - Races of Skyrim"
sModDescription105 = "Patch for Imperious - Races of Skyrim. Patch by SkyLover264."
sModImage105 = "fomod\\images\\IMP.jpg"
If GetEspmStatus("Imperious - Races of Skyrim.esp") > 1
	sModName105 = "|" + sModName105
EndIf

sModName106= "The Magic of Food and Drink"
sModDescription106 = "Patch for The Magic of Food and Drink. Patch by MisterB1969."
sModImage106 = "fomod\\images\\MFD.jpg"
If GetEspmStatus("TheMagicOfFoodAndDrink.esp") > 1
	sModName106 = "|" + sModName106
EndIf

sModName107= "Organized Bandits In Skyrim \(OBIS\)"
sModDescription107 = "Patch for Organized Bandits In Skyrim \(OBIS\). Patch by SirJesto."
sModImage107 = "fomod\\images\\OBIS.jpg"
If GetEspmStatus("OBIS SE.esp") > 1
	sModName107 = "|" + sModName107
EndIf

sModName108= "Opulent Outfits - All In One"
sModDescription108 = "Patch for Opulent Outfits \(All In One\). Patch by SirJesto."
sModImage108 = "fomod\\images\\OPO.jpg"
If GetEspmStatus("OpulentOutfits_2017-SSE AIO.esp") > 1
	sModName108 = "|" + sModName108
EndIf

sModName109= "Opulent Outfits - Replacer"
sModDescription109 = "Patch for Opulent Outfits \(Replacer\). Patch by SirJesto."
sModImage109 = "fomod\\images\\OPO.jpg"
If GetEspmStatus("OpulentOutfits_2017-SSE Replacer.esp") > 1
	sModName109 = "|" + sModName109
EndIf

sModName110= "Sands of Time"
sModDescription110 = "Patch for Sands of Time. Patch by SirJesto."
sModImage110 = "fomod\\images\\SOT.jpg"
If GetEspmStatus("SleepingDangers-SandsofTime.esp") > 1
	sModName110 = "|" + sModName110
EndIf


sModName111= "SkyTEST - Harder Creatures"
sModDescription111 = "Patch for SkyTEST - Harder Creatures. Patch by SirJesto."
sModImage111 = "fomod\\images\\STHC.jpg"
If GetEspmStatus("SkyTEST-HarderCreatures.esp") > 1
	sModName111 = "|" + sModName111
EndIf

sModName112= "Vampire Lord and Werewolf Hybrid"
sModDescription112 = "Patch for Vampire Lord and Werewolf Hybrid.\n\nIf you prefer Yorimi\'s take on the Companions requirements over Taj\'s \(Slower Companions Advancement\) Use the optional file on the mod page instead. Patch by SkyLover264."
sModImage112 = "fomod\\images\\VLWH.jpg"
If GetEspmStatus("VLW_Hybrid.esp") > 1
	sModName112 = "|" + sModName112
EndIf

sModName113= "Windsong Immersive Character Overhaul \(WICO\)"
sModDescription113 = "Patch for Windsong Immersive Character Overhaul (WICO). Patch by SirJesto."
sModImage113 = "fomod\\images\\WICO.jpg"
If GetEspmStatus("WICO - Immersive People.esp") > 1
	sModName113 = "|" + sModName113
EndIf


; User selection of compatibility add-ons
SelectMany "Nexus Skyrim Overhaul - Compatibility Patches\n\nThe following mods have been detected in your Skyrim SE load order.\nPlease feel free to select or de-select any mod based on your preference.\n\nYou may click to read the description for each patch before installing",\
		str(sModName101),str(sModDescription101),str(sModImage101),\
		str(sModName102),str(sModDescription102),str(sModImage102),\
		str(sModName103),str(sModDescription103),str(sModImage103),\
		str(sModName104),str(sModDescription104),str(sModImage104),\
		str(sModName105),str(sModDescription105),str(sModImage105),\
		str(sModName106),str(sModDescription106),str(sModImage106),\
		str(sModName107),str(sModDescription107),str(sModImage107),\
		str(sModName108),str(sModDescription108),str(sModImage108),\
		str(sModName109),str(sModDescription109),str(sModImage109),\
		str(sModName110),str(sModDescription110),str(sModImage110),\
		str(sModName111),str(sModDescription111),str(sModImage111),\
		str(sModName112),str(sModDescription112),str(sModImage112),\
		str(sModName113),str(sModDescription113),str(sModImage113)
		
	Case "Replacement esp without Stones of Barenziah Markers"
		SelectSubPackage '01 NSB'
		Break
	Case "Slower Companions Advancement - Original Requirements"
		SelectSubPackage '01 SCA'
		Break
	Case "Advanced Adversary Encounters"
		SelectSubPackage '02 AAE'
		Break
	Case "Elemental Destruction Magic"
		SelectSubPackage '02 ELD'
		Break
	Case "Imperious - Races of Skyrim"
		SelectSubPackage '02 IMP'
		Break
	Case "The Magic of Food and Drink"
		SelectSubPackage '02 MFD'
		Break
	Case "Organized Bandits In Skyrim \(OBIS\)"
		SelectSubPackage '02 OBIS'
		Break
	Case "Opulent Outfits - All In One"
		SelectSubPackage '02 OPOAIO'
		Break
	Case "Opulent Outfits - Replacer"
		SelectSubPackage '02 OPOREP'
		Break
	Case "Sands of Time"
		SelectSubPackage '02 SOT'
		Break
	Case "SkyTEST - Harder Creatures"
		SelectSubPackage '02 STHC'
		Break
	Case "Vampire Lord and Werewolf Hybrid"
		SelectSubPackage '02 VLWH'
		Break
	Case "Windsong Immersive Character Overhaul \(WICO\)"
		SelectSubPackage '02 WICO'
		Break
EndSelect
;== Finish Page ==
Note "Thank you for installing Nexust Skyrim Overhaul Patches\n\n\nConfirm your selections above - if you are not happy with the selection use the BACK button below.\nWhen ready, tick 'Apply these selections' Below, and then click the 'Finish' button.\n\nIf You Have Auto-Anneal/Install Wizards set in Wrye Bash preferences, the Wizard will install your selections after clicking Finish\nOtherwise, right-click the installer again and choose Install"